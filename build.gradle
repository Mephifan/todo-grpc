buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

plugins {
  id "com.google.protobuf" version "0.8.5"
  id "java"
  id "application"
  id "idea"
  id 'com.github.ben-manes.versions' version '0.19.0'
}

group 'com.adcubum.syrius.grpc'
version '1.0-SNAPSHOT'

protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.6.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.12.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.12.0'
    compile 'com.google.api-client:google-api-client:1.25.0'
    compile 'io.grpc:grpc-netty-shaded:1.16.1'
    compile 'io.grpc:grpc-protobuf:1.16.1'
    compile 'io.grpc:grpc-stub:1.16.1'
    compile 'com.google.api.grpc:googleapis-common-protos:0.0.3'
    compile 'org.xerial:sqlite-jdbc:3.21.0'
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.adcubum.syrius.grpc.TaskServer'
}

startScripts.enabled = false

task taskServer(type: CreateStartScripts) {
    mainClassName = "com.adcubum.syrius.grpc.TodoServiceGrpc"
    applicationName = "task-server"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(taskServer)
    fileMode = 0755
}
